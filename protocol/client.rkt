#lang racket

(require racket/udp)
(include "globals.rkt")

(provide request-file)

(define (add-pkt bstr-pkt base pkts)
  (define (bstr-pkt->pkt bstr-pkt)
    (cons (extract-seq-num bstr-pkt) (list (subbytes bstr-pkt PKT-HEADER-SIZE))))
  (define pkt (bstr-pkt->pkt bstr-pkt))
  (if (or (assoc (first pkt) pkts) (< (first pkt) base))
    pkts
    (cons pkt pkts)))

(define (request-file [output-file-name OUTPUT-FILE])
  (define sock (udp-open-socket))
  (define out-file (open-output-file output-file-name #:exists 'replace))
  (define buf (make-bytes PKT-SIZE))
  (define (writer base pkts)
    (cond ((empty? pkts) (receiver base pkts))
          ((= base (caar pkts))
           (write-bytes (second (car pkts)) out-file)
           (writer (add1 base) (cdr pkts)))
          (else (receiver base pkts))))
  (define (receiver base pkts [time 0])
    (match-define-values (num-bytes _ _) (udp-receive!* sock buf))
    (cond (num-bytes
            (define bstr-pkt (subbytes buf 0 num-bytes))
            (define seq-num (extract-seq-num bstr-pkt))
            (cond ((data-bstr-pkt? bstr-pkt)
                   (send-to-server sock (make-header seq-num ACK))
                   (if (= seq-num base)
                     (writer base (sort (add-pkt bstr-pkt base pkts) < #:key car))
                     (receiver base (add-pkt bstr-pkt base pkts))))
                  ((fin-bstr-pkt? bstr-pkt)
                   (write-bytes (apply bytes-append (map second (sort pkts < #:key car))) out-file)
                   (close-output-port out-file) 
                   (send-to-server sock (make-header 0 ACK))
                   (displayln "File saved. Shutting down client."))
                  (else (receiver base pkts))))
          (else (cond ((and (= base 0)
                            (empty? pkts)
                            (< (+ TIMEOUT time) (current-seconds)))
                       (send-to-server sock (make-header 0 SYN))
                       (receiver base pkts (current-seconds)))
                      (else (receiver base pkts time))))))
  (udp-bind! sock ADDR CLIENT-PORT)
  (udp-set-receive-buffer-size! sock (max (* 1024 1024) (* 2 PKT-SIZE WINDOW-SIZE)))
  (send-to-server sock (make-header 0 SYN))
  (receiver 0 '()  (current-seconds)))
